plugins {
    id 'java'
    id "org.xbib.gradle.plugin.jflex" version "1.6.0" // a plugin for building a lexer using jflex
    id "cup.gradle.cup-gradle-plugin" version "2.0" // building a parser using cup
    id "idea"
    id 'application'
}

version '0.5.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(18)
    }
}

ant.importBuild("sherrloc/build.xml") {
    antTargetName -> "slc-" + antTargetName
}

repositories {
    mavenCentral()
    flatDir {
        dirs "build/libs"
    }
}

def compileSlcJar = tasks.register("compileSlcJar", Copy) {
    from(ant.lib) {
        include "SHErrLoc.jar"
        rename "SHErrLoc.jar", "sherrloc.jar"
    }
    into "build/libs"
}

compileSlcJar.configure {
    dependsOn("slc-jar")
}

sourceSets {
    main {
        jflex {
            srcDir "src"
        }
        java {
            srcDirs = ['src', 'build/generated-src/cup']
        }
        resources {
            srcDirs = ['src/config', 'resources']
        }
    }
    test {
        java {
            srcDirs = ['test/src']
        }
        resources {
            srcDirs = ['test/contracts']
        }
    }
}

cup {
    sourceDir = "src"
    args = ["-expect", "1000", "-interface", "-parser", "Parser"]
}

idea {
    module {
        generatedSourceDirs += file("build/generated-src/cup")
    }
}

compileJava.configure {
    dependsOn("compileSlcJar")
}

def fatJar = tasks.register("fatJar", Jar) {
    manifest {
        attributes "Main-Class": "SCIF"
        attributes 'Multi-Release': 'true'
    }

    archiveBaseName = "SCIF-full"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

test {
    useJUnitPlatform()
}

artifacts {
    archives fatJar
}

configurations {
    priority // hack to force sherrloc to apprear later than jflex and cup
    sourceSets.main.compileClasspath = configurations.priority + sourceSets.main.compileClasspath
    sourceSets.main.runtimeClasspath = configurations.priority + sourceSets.main.runtimeClasspath
    sourceSets.test.compileClasspath = configurations.priority + sourceSets.test.compileClasspath
    sourceSets.test.runtimeClasspath = configurations.priority + sourceSets.test.runtimeClasspath
}

dependencies {
    implementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    priority "de.jflex:jflex:1.8.2"
    priority 'de.jflex:cup-parent:11b-20160615'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
    implementation 'com.owlike:genson:1.5'
    implementation "info.picocli:picocli:4.0.0"
    implementation files("build/libs/sherrloc.jar")
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
}

def dummyOutputStream = new OutputStream() {
    @Override
    public void write(int b) {}
}

application {
    mainClass = "SCIF"
}

run {
    enableAssertions = true
}